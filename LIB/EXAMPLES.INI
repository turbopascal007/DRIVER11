# Example file that can be included in your player.ini
#
# It consists of three sections:
# - 1. Introduction to the language
# - 2. Multitasking
# - 3. Heartbeat

unit player

write **loading examples.ini...^

# 1. Introduction to the language
#
# An example of how to use some constructs in this language
# .. to invoke this command, type 'status' at the prompt

verb status

# If my prename is alphabetically greater than 'ghost' (in this case
# 'ghost of') write that I am immaterial at the moment

  if prename > ghost
    write You are immaterial!!^
  endif

# If my hitpoints are below 100 I am damaged, otherwise healthy

  if hp < 100
    write You are a little damaged..^
  else
    write You are still healthy!!^
  endif

# Check if I have got some money with me

  if money > 0
    write You even own some money :)^
  endif

# Now with a boolean construct:

  if (prename > gho) and (hp > 5)
    write You are ghost, but you are ready to be alive again..^
  endif

# Complete this verb declaration

endverb


# Another example with assignment of variables and a while loop:

verb heal
  if hp = 100
    write You are already healed!^
  else
    while (hp < 101)
      inc hp 1
      write .
    endwhile
    write You are healed!^
  endif
endverb


# 2. Multitasking
#
# Two test verbs to check multitasking capabilities of the driver.
#
# The count loop will take quite some time, use this to check system
# load for a generic task.
#
# The forever loop will loop forever, use this to check system
# overhead for an empty task.

verb count
  int count 0
  while (count < 25000)
    writevar count
    write ~~
    inc count 1
  endwhile
endverb

verb forever
  int a 0
  while (a = 0)
  endwhile
endverb


# 3. Heartbeat
#
# The verb 'hb' is called by the driver in players everytime a heartbeat occurs
# You could use this for special purposes like in the example below.
#
# 'superheal' will heal you much faster.
# 'hpmeter' will report your health every heartbeat.

int hbcount 0
int superheal 0
int hpmeter 0

verb hb
  if ( superheal = 1 ) then
    inc hbcount 1
    if ( hbcount > 3 ) then
      inc hp 10
      set hbcount 0
      if ( hp > 100 ) then
        inc hp -10
      endif
    endif
  endif
  if ( hpmeter = 1 ) and ( prename < ghost ) then
    write Hitpoints:~
    writevar hp
    write ~(
    writevar maxhp
    write )      Experience:~
    writevar exp
    write ~~~~~~~~Money:~
    writevar money
    write ^
  endif
endverb

verb superheal
  if ( superheal = 0 ) then
    set superheal 1
    write You now have superheal on! (10hp/3hb)^
  else
    set superheal 0
    write Superheal turned off^
  endif
endverb

verb hpmeter
  if ( hpmeter = 0 ) then
    set hpmeter 1
    write You turned hpmeter on^
  else
    set hpmeter 0
    write You turned hpmeter off^
  endif
endverb
